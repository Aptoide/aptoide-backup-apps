apply plugin: 'com.android.application'

android {
  signingConfigs {
    release {
      storeFile = file(project.STORE_FILE_BACKUP_UPLOADER)
      storePassword = project.STORE_PASSWORD_BACKUP_UPLOADER
      keyAlias = project.KEY_ALIAS_BACKUP
      keyPassword = project.KEY_PASSWORD_BACKUP
      //            v2SigningEnabled = false
    }
  }

  compileSdkVersion 26
  useLibrary 'org.apache.http.legacy'

  defaultConfig {
    applicationId "pt.aptoide.backupapps"
    minSdkVersion 15
    targetSdkVersion 26
    versionCode project.VERSION_CODE_BACKUP.toInteger()
    versionName "2.4.1"

    manifestPlaceholders = [LOCALYTICS_APP_KEY: project.LOCALYTICS_RELEASE_KEY_BACKUP,
                            fabricApiKey      : project.FABRIC_API_KEY]

    resValue "string", "facebook_app_id", "\"" + project.FACEBOOK_APP_ID_BACKUP + "\""

    // OAuth server id for Google Plus Sign In
    buildConfigField "String", "GOOGLE_OAUTH_SERVER_ID", "\"" + project.GMS_SERVER_ID + "\""
  }

  buildTypes {

    debug {
      applicationIdSuffix '.debug'
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    // Lots of issues with the project..
    abortOnError false
  }

  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      def project = "backup"
      def SEP = "_"
      def buildType = variant.buildType.name
      def versionName = variant.versionName
      def versionCode = variant.versionCode

      def newApkName = project + SEP + buildType + SEP + versionName + SEP + versionCode + ".apk"

      output.outputFileName = newApkName
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation 'com.android.support:appcompat-v7:26.1.0'

  implementation 'com.google.android.gms:play-services-basement:9.8.0'
  implementation 'com.google.android.gms:play-services-auth:9.8.0'
  implementation 'com.google.android.gms:play-services-plus:9.8.0'

  implementation 'com.facebook.android:facebook-android-sdk:6.0.0'

  implementation 'com.squareup:otto:1.3.8'

  implementation 'com.nostra13.universalimageloader:universal-image-loader:1.8.6'

  implementation project(':MultiChoiceAdapter')

  implementation project(':ViewPagerIndicator')

  implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
    transitive = true;
  }

  implementation files('libs/localytics.jar')
}

